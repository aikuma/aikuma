apply plugin: 'eclipse'

subprojects {
    apply plugin: 'scala'
    apply plugin: 'maven-publish'

    group = 'org.lp20'

    sourceCompatibility = '1.6'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.scala-lang:scala-library:2.11.7'
        compile('com.googlecode.json-simple:json-simple:1.1.1') {
            exclude module: 'junit'
        }
    }

    sourceSets.main.scala {
        srcDir project.projectDir
    }

    compileScala {
        scalaCompileOptions.fork = true
        scalaCompileOptions.useAnt = false
    }
}

project(':api') {
    buildDir file(project.rootDir.path + '/build/api')
    jar.baseName = 'aikuma-cloud-storage'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = jar.baseName
            }
        }
    }
}

project(':demo') {
    buildDir file(project.rootDir.path + '/build/demo')
    dependencies {
        compile project(':api')
    }

    task fatJar(type: Jar) {
        baseName = jar.baseName + '-all'
        from { configurations.compile.collect {it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
}

